###############################################################################
# Catch2 dependency
###############################################################################

# TODO: spell the dependency canonically using 'find_package',
# and possibly reroute using a dependency provider.
include(${PROJECT_SOURCE_DIR}/cmake/deps/Catch2.cmake)
include(${PROJECT_SOURCE_DIR}/cmake/TestUtils.cmake) # aoc_add_tests

###############################################################################
# Library target
###############################################################################
add_library(aoc.unit_test INTERFACE)
add_library(aoc::unit_test ALIAS aoc.unit_test)

target_sources(aoc.unit_test INTERFACE
  FILE_SET HEADERS
  BASE_DIRS ${PROJECT_SOURCE_DIR}
  FILES catch2.hpp
)
set_target_properties(aoc.unit_test PROPERTIES
  VERIFY_INTERFACE_HEADER_SETS ${AOC_VERIFY_HEADERS}
)
target_link_libraries(aoc.unit_test INTERFACE
  Catch2::Catch2WithMain
)
# NB: as long as 'aoc.unit_test' is 'INTERFACE', we cannot really link it to
# 'aoc::project_warnings' when its headers are verified, as the "verify" target
# generated by cmake cannot be used directly.
# Probably the "best" way to do so is to make the target 'STATIC', with only
# one, empty sosurce file. Not sure it is worth it.
